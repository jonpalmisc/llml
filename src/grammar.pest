WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

Backslash = _{ "\\" }
OpenBrace = _{ "{" }
CloseBrace = _{ "}" }
OpenBracket = _{ "[" }
CloseBracket = _{ "]" }
DoubleQuote = _{ "\"" }

SpecialChar = _{(
  Backslash |
  OpenBrace |
  CloseBrace
)}

Alphanumeric = _{ ASCII_ALPHANUMERIC }
Literal = @{ (!SpecialChar ~ ANY)+ }

AttributeKey = { Alphanumeric+ }
AttributeValue = { (!DoubleQuote ~ ANY)* }
Attribute = ${ AttributeKey ~ "=" ~ DoubleQuote ~ AttributeValue ~ DoubleQuote }
AttributeList = { OpenBracket ~ Attribute+ ~ CloseBracket }

ElementName = { Alphanumeric+ }
ElementClass = @{ "." ~ (Alphanumeric | "-" | "_")+ }
ElementContent = _{ (Literal | Element)+ }
Element = {
  Backslash ~
  ElementName ~
  ElementClass* ~
  AttributeList? ~
  OpenBrace ~
  ElementContent ~
  CloseBrace
}

File = { SOI ~ Element+ ~ EOI }