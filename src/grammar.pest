WHITESPACE = _{ "\r" | "\n" }

Backslash = _{ "\\" }
Exclamation = _{ "!" }
OpenBrace = _{ "{" }
CloseBrace = _{ "}" }
OpenBracket = _{ "[" }
CloseBracket = _{ "]" }
DoubleQuote = _{ "\"" }
Space = _{ " "* }

SpecialChar = _{(
  Backslash |
  OpenBrace |
  CloseBrace
)}

Alphanumeric = _{ ASCII_ALPHANUMERIC }
Literal = @{ (!SpecialChar ~ ANY)+ }

OpenSeq = _{ OpenBrace ~ Space }
CloseSeq = _{ Space ~ CloseBrace }

MacroName = { Alphanumeric+ }
MacroArgument = _{ OpenSeq ~ (Literal | Element) ~ CloseSeq }
MacroCall = {
  Backslash ~
  MacroName ~
  Exclamation ~
  MacroArgument+
}

AttributeKey = { Alphanumeric+ }
AttributeValue = { (!DoubleQuote ~ ANY)* }
Attribute = ${ AttributeKey ~ "=" ~ DoubleQuote ~ AttributeValue ~ DoubleQuote }
AttributeList = _{ OpenBracket ~ (Attribute ~ Space)+ ~ CloseBracket }

ElementName = { Alphanumeric+ }
ElementClass = @{ "." ~ (Alphanumeric | "-" | "_")+ }
ElementContent = _{ (Literal | Element | MacroCall)* }
Element = {
  Backslash ~
  ElementName ~
  ElementClass* ~
  AttributeList? ~
  OpenSeq ~
  ElementContent ~
  CloseSeq
}

File = { SOI ~ (MacroCall | Element)+ ~ EOI }