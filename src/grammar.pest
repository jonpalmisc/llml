WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

Backslash = _{ "\\" }
Pipe = _{ "|" }
OpenBrace = _{ "{" }
CloseBrace = _{ "}" }
OpenBracket = _{ "[" }
CloseBracket = _{ "]" }
DoubleQuote = _{ "\"" }

SpecialChar = _{(
  Pipe |
  Backslash |
  OpenBrace |
  CloseBrace
)}

Alphanumeric = _{ ASCII_ALPHANUMERIC }
Literal = @{ (!SpecialChar ~ ANY)+ }

MacroName = { Alphanumeric+ }
MacroArgument = { OpenBrace ~ Literal ~ CloseBrace }
MacroCall = {
  Pipe ~
  MacroName ~
  MacroArgument+
}

AttributeKey = { Alphanumeric+ }
AttributeValue = { (!DoubleQuote ~ ANY)* }
Attribute = ${ AttributeKey ~ "=" ~ DoubleQuote ~ AttributeValue ~ DoubleQuote }
AttributeList = _{ OpenBracket ~ Attribute+ ~ CloseBracket }

ElementName = { Alphanumeric+ }
ElementClass = @{ "." ~ (Alphanumeric | "-" | "_")+ }
ElementContent = _{ (Literal | Element | MacroCall)+ }
Element = {
  Backslash ~
  ElementName ~
  ElementClass* ~
  AttributeList? ~
  OpenBrace ~
  ElementContent ~
  CloseBrace
}

File = { SOI ~ Element+ ~ EOI }